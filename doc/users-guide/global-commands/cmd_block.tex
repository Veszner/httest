% =========================================
% COMMAND: BLOCK
% =========================================

\newpage
\section{BLOCK}
\label{cmd:BLOCK}

\paragraph{Syntax:}
\subparagraph{}
\texttt{BLOCK <name>} 

\paragraph{Purpose:}
\subparagraph{}
Begins a body with scripting code that can be invoked from anywhere 
else directly, just like a command. In earlier versions, the command \texttt{\_CALL} was
needed to invoke a block; this style is still supported, but not 
necessary anymore.

The \texttt{BLOCK} must be closed using the \texttt{END} command.

\section{Parameters and Return Values}
\subsection{Signature}
A block can define one or multiple parameters, and also one or multiple return values. 
Parameters and return values are separated by a ":". Parameters and return value list are separated with spaces.
\begin{usplisting}
    # Block signature
    BLOCK myFunction myP mySecOne andThird : firstRet secOne
      _DEBUG \$myP
      _DEBUG \$mySecOne
      _DEBUG \$andThird
      _SET firstReturn=to any value
      _SET secondOne=to another value
    END 

    CLIENT
      myFunction aVal "Another one" "not enough" RET RETVAL2
      _DEBUG \$RET
      _DEBUG \$RETVAL2
    END
\end{usplisting}

\pagebreak 
\subsection{No Block Signature}
If there are no parameters and return values defined, parameters can be 
accessed with \texttt{\$1..\$n}, similar to the way a shell script 
accesses its parameters.
\begin{usplisting}
    # No block signature
    BLOCK myFunction
      _DEBUG \$1
      _DEBUG \$2
      _DEBUG \$3
      _SET \$4=A return value
      _SET \$5=And another
    END

    CLIENT
      myFunction aValue "Another one" "still not enough" RET RETVAL2
      _DEBUG \$RET
      _DEBUG \$RETVAL2
    END
\end{usplisting}

\subsection{Spaces in Variable Values}
The httest engine resolves parameter variables \textit{before} the block 
is actually invoked. Since parameters are separated by blanks, this means 
that one single variable with a value like "\texttt{Hello World}" will be handed over 
to the block as two separate parameters, "\texttt{Hello}" in \$1 and "\texttt{World}" in \$2. 
To prevent this, the variable used for a parameter must be quoted, e.g.:
\begin{usplisting}
    # Spaces in variable 'myStuff'
    BLOCK myFunction p1 p2 p3
      _DEBUG \$p1
      _DEBUG \$p2
      _DEBUG \$p3
    END 

    CLIENT
      _SET myStuff=Hello World
      myFunction "\$myStuff" "next param" andStillNotEnoughParams
    END
\end{usplisting}

\pagebreak 
\subsection{Spaces and Quotes in Variable Values}
To complicate things further, a parameter variable may not only contain blanks, 
it might even contain quotes; in such a case, simply quoting the parameter will 
not work, since the quotes around the parameter will get in conflict with the 
quotes from the resolved variable.

\subparagraph{}
For cases like that, there is a way to prevent variable resolution before 
a block is invoked: by enclosing the parameter in \texttt{VAR(<param name>)}.
\begin{usplisting}
    # Spaces and quotes in variable 'myStuff'
    BLOCK myFunction p1 p2 p3
      _DEBUG \$p1
      _DEBUG \$p2
      _DEBUG \$p3
    END 

    CLIENT
      _SET myStuff=Hello World "some quotes" messup your parameters
      myFunction VAR(myStuff) "next param" andStillNotEnoughParams
    END
\end{usplisting}

\subparagraph{}
So, in all cases where values of parameters for blocks \textit{might} 
contain blanks or quotes, it is generally a good idea and practice to use the 
\texttt{VAR()} function.

\subsection{Error Handler}
A special, reserved name for a \texttt{BLOCK} is \texttt{ON\_ERROR}. It 
designates the block to be invoked if an unexpected error occurs during 
a test. The block can then handle the error, output some information etc. 
\begin{usplisting}
    # A simple error handler example
    BLOCK ON_ERROR
    _DEBUG
    _DEBUG !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    _DEBUG An error occurred, please fix it and try again!
    _DEBUG !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
    _DEBUG
    _EXIT FAILED
    END
\end{usplisting}

\pagebreak 
\subsection{Finally Handler}
A special, reserved name for a \texttt{BLOCK} is \texttt{FINALLY}. It 
designates the block to be invoked on test termination.  The block can 
then handle cleanup stuff like restarting a server, removing generated
resources during test or logout from a service.
\begin{usplisting}
    # A simple finally handler example
    BLOCK FINALLY
    _EXEC server restart
    _EXEC rm -f test.txt
    END
\end{usplisting}

