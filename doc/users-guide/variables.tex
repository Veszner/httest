\chapter{htt Variables}
\label{chap:variables}

Within a htt script, variables can store values read from files 
or created during the execution of the script.

\section{Variable Names}

A variable has a name and a value. Variable names are 
case-sensitive. This is new to httest 2.1!

\begin{usplisting}
    CLIENT
    # Set variable with value "First"
    _SET VarA=First
    
    # This will not overwrite the value of the 
    # variable "VarA" any more.
    _SET VARA=Second
    END
\end{usplisting}

\section{Variable Scope}

A variable is defined either as a global variable, or as a local within 
a body (like \texttt{CLIENT}, \texttt{SERVER} etc.). Note that unlike as in  
common programming languages, local variables created in a \texttt{BLOCK} 
are also available in the invoking bodies.

So, if a \texttt{CLIENT} body invokes a \texttt{BLOCK} where a local variable "A" 
is created, that variable will also become available in the invoking \texttt{CLIENT} 
body.

\newpage
\section{Setting A Variable} 

To define a variable as global (and initialize with an empty value):

\begin{usplisting}
    GLOBAL global_var
\end{usplisting}
If $<$global\_var$>$ is assigned with the \texttt{\_SET} command in a \texttt{CLIENT},
\texttt{SERVER} or \texttt{DAEMON} thread, then the new value will be seen in
the others, as well.

To define and initialize a global variable which can be "shaded":

\begin{usplisting}
    SET shaded_global_var=initial_value
\end{usplisting}

If $<$shaded\_global\_var$>$ is assigned with the \texttt{\_SET} command in a \texttt{CLIENT}, \texttt{SERVER} or \texttt{DAEMON} thread, then the new value will NOT be seen in the others, because then in fact a new local variable is created with the same name which "shades" $<$shaded\_global\_var$>$. The other threads will still see the $<$initial\_value$>$ of $<$shaded\_global\_var$>$ variable.

The syntax for setting a local variable in a body is:

\begin{usplisting}
    _SET name=value
\end{usplisting}

Examples:

\begin{usplisting}
    SET MYVAR=SomeValue
    
    CLIENT
    _SET MYLOCAL=LocalValue
    # Invoke block DOIT, makes variable "DoLocal"
    # available in this CLIENT body as well.
    _CALL DOIT
    END
    
    BLOCK DOIT
    _SET DoLocal=anotherValue
    END
\end{usplisting}

\newpage
\section{Using Variables}

In order to resolve a variable and use the value it is holding, 
its name must be prefixed with a "\char`\$" character, just like 
in a shell script. Example:

\begin{usplisting}
    # Set global Variables for host and port
    SET MyHost=www.acme.com
    SET MyPort=8080
    
    CLIENT
    # Invoke named block DOIT which
    # sets the variable for the URI path
    _CALL DOIT
    
    _REQ $MyHost $MyPort
    __GET $UriPath HTTP/1.1
    __Host: www.acme.com
    __
    _WAIT
    END
    
    BLOCK DOIT
    # Set URI path variable which will also
    # become available in the invoking body.
    _SET UriPath=/webapp
    END
\end{usplisting}

